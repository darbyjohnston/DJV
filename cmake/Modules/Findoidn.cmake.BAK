# Find the OpenImageDenoise library.
#
# This module defines the following variables:
#
# * oidn_FOUND
# * oidn_INCLUDE_DIRS
# * oidn_LIBRARIES
#
# This module defines the following imported targets:
#
# * oidn::OpenImageDenoise
#
# This module defines the following interfaces:
#
# * oidn

find_package(ISPC REQUIRED)
find_package(TBB REQUIRED)

find_path(oidn_INCLUDE_DIR NAMES OpenImageDenoise/oidn.h)
set(oidn_INCLUDE_DIRS
    ${oidn_INCLUDE_DIR}
    ${TBB_INCLUDE_DIRS})

find_library(oidn_LIBRARY NAMES OpenImageDenoise)
set(oidn_LIBRARIES
    ${oidn_LIBRARY}
    ${TBB_LIBRARIES})

include(FindPackageHandleStandardArgs)
find_package_handle_standard_args(
    oidn
    REQUIRED_VARS oidn_INCLUDE_DIR oidn_LIBRARY)
mark_as_advanced(oidn_INCLUDE_DIR oidn_LIBRARY)

if(oidn_FOUND AND NOT TARGET oidn::OpenImageDenoise)
    add_library(oidn::OpenImageDenoise UNKNOWN IMPORTED)
    set_target_properties(oidn::OpenImageDenoise PROPERTIES
        IMPORTED_LOCATION "${oidn_LIBRARY}"
        INTERFACE_COMPILE_DEFINITIONS oidn_FOUND
        INTERFACE_INCLUDE_DIRECTORIES "${oidn_INCLUDE_DIR}"
        INTERFACE_LINK_LIBRARIES "TBB")
endif()
if(oidn_FOUND AND NOT TARGET oidn)
    add_library(oidn INTERFACE)
    target_link_libraries(oidn INTERFACE oidn::OpenImageDenoise)
endif()
